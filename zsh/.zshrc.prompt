setopt PROMPT_SUBST

autoload -U colors && colors

black="%{$fg_bold[black]%}"
gray="%{$fd[white]%}"
grey="%{$fd[white]%}"
red="%{$fg_bold[red]%}"
d_green="%{$fg[green]%}"
green="%{$fg_bold[green]%}"
yellow="%{$fg_bold[yellow]%}"
blue="%{$fg_bold[blue]%}"
purple="%{$fg_bold[magenta]%}"
magenta="%{$fg_bold[magenta]%}"
cyan="%{$fg_bold[cyan]%}"
white="%{$fg_bold[white]%}"
end="%{$reset_color%}"

last_cmd_succ="%(?.${black}.${red})|${end}"

function dir_stacks () {
    depth=`dirs -p | wc -l | tr -d ' '`
    if [[ $depth -gt 1 ]]
    then
        echo -n "${d_green}"
        printf '['%.0s {2..${depth}}
        #for (( i = 1; i < $depth; i += 1))
        #do 
        #    echo -n "["
        #done
    fi
}

function current_path () {
    echo "$(dir_stacks)${green}[%~]${end}"
}

function git_branch_name () {
    branch_name=$(git symbolic-ref HEAD 2>/dev/null)

    if [[ -n $branch_name ]]
    then
        if [[ "`git status --porcelain`" == "" ]]
        then
            echo "${blue}(${branch_name#refs/heads/})${end}"
        else
            echo "${red}(${branch_name#refs/heads/})${end}"
        fi
    fi
}

suspend_jobs="%1(j.${blue}jobs:%j${end}.)"

screen_str=""
if [[ "$TERM" == 'screen' ]]
then
    session_name="`echo $STY | cut -d '.' -f2`"
    screen_str="%{[1;35m[$session_name W$WINDOW][m%}"
fi

uid_str="%(!.#.>)"

vim_mode_str="${black}[zsh]${end}"

function zle-line-finish {
    vim_mode_str="${black}[zsh]${end}"
}
function zle-keymap-select {
    case ${KEYMAP} in
    vicmd)
        vim_mode_str="${purple}:zsh:${end}"
        ;;
    viins|main)
        vim_mode_str="${black}[zsh]${end}"
        ;;
    esac
    zle reset-prompt
}
zle -N zle-line-finish
zle -N zle-keymap-select

prompt='${last_cmd_succ}$(current_path)$(git_branch_name)${suspend_jobs}${vim_mode_str}
${last_cmd_succ}${cyan}%T${end}${yellow}%n${end}@${white}%m${end}${screen_str}${uid_str} '

